
json = require "json"
assert(json)
typecheck = assert(SMODS.load_file("libs/steamodded_test/libs/share/lua/5.1/typecheck/init.lua")())
argcheck = typecheck.argcheck
argscheck = typecheck.argscheck
ui_map = assert(SMODS.load_file("libs/balatro_ui_map/main.lua"))()

logger = { -- Placeholder logger, for when DebugPlus isn't available
    log:   print
    debug: print
    info:  print
    warn:  print
    error: print
}

dbp_success, dpAPI = pcall(require, "debugplus-api")
debugplus = nil
dbp_logger_dot_lua = nil
if dbp_success and dpAPI.isVersionCompatible(1)
    debugplus = dpAPI.registerID("steamodded_test")
    logger = debugplus.logger
    -- dbp_logger_dot_lua = assert(SMODS.load_file('debugplus/logger.lua', "DebugPlus"))()

    -- Hopefully, this is in there: https://github.com/WilsontheWolf/DebugPlus/pull/24
    -- Otherwise it makes a dummy that's less good
    -- I tried to just do this with Lovely, and maybe I'm just using it wrong, but it doesn't seem to want to inject into other mods
    logger.advanced = logger.advanced or (data, ...) ->
        -- Trim the first arg, because it's always gonna just be "[test]", and there's already "[steamodded_test]" at the start of each of these
        args = {...}
        args = [arg for arg in *args[2,]]
        logger[string.lower(data.level)](unpack args)
    -- dbp_logger_dot_lua.registerLogHandler()

-- Internal stuff

_concat_lists = (list_of_lists) ->
    output = {}
    for _, list in ipairs(list_of_lists)
        for _, item in ipairs(list)
            output[] = item
    return output

---@param this_guy string | string[]
---@return string[]
_put_in_array_if_alone = (this_guy) ->
    local output
    if type(this_guy) == "function" or type(this_guy) == "string"
        output = {this_guy}
    else
        output = this_guy
    output

_should_event_count = (event) ->
    -- Stuff like juice_card_until, which just kind of goes on forever and doesn't matter
    if event.blocking == false and event.blockable == false
        return false

    return true

_get_event_count = () ->
    return #[event for event in *G.E_MANAGER.queues.base when _should_event_count(event)]
--     total = 0
--     for event in *G.E_MANAGER.queues.base
--         count_it = true
--         for ignore_if in *ignore_ifs
--             if ignore_if(event)
--                 count_it = false
--                 break
--         if count_it
--             total += 1
--     return total

_run_ordered_events = (funcs, previous_result=nil, retry_count=0) ->
    if #funcs == 0
        return

    -- So sometimes you end up with two of these at once
    -- How? zero clue
    -- This is not a good way to solve this but tbh I'm SO done
    if #[event for event in *G.E_MANAGER.queues.base when event.steamodded_test] > 1
        return

    first_func = funcs[1]
    remaining_funcs = [func for func in *funcs[2,]]
    
    -- Unpack, if necessary
    if type(first_func) == "table"
        -- print("Unpacking #{first_func}")
        funcs = _concat_lists({
            first_func,
            remaining_funcs
        })
        first_func = funcs[1]
        remaining_funcs = [func for func in *funcs[2,]]

    event = Event({
        no_delete: true
        func: ->
            -- If we're not ready yet, delay
            retry_danger_threshold = 100
            event_count = _get_event_count()
            event_tolerance = (retry_count-1) / retry_danger_threshold + 2 -- This one will always be in there, it's not removed until a success. and there seems to be some other permanent one?
            if event_count > event_tolerance 
                if retry_count > 0 and retry_count % retry_danger_threshold == 0
                    logger.warn("Hit #{retry_count} retries, there's probably something stuck in the queue? (The queue has #{#G.E_MANAGER.queues.base} entries) (raising the remaining event tolerance to #{math.ceil(event_tolerance)})")
                _run_ordered_events(funcs, previous_result, retry_count+1)
            -- Actually do it :)
            else
                -- logger.debug("Running event #{_}, #{func} (#{previous_result})")
                result = nil
                success, result = pcall(-> first_func(previous_result))
                if not success
                    logger.error(result)
                    result = "FUCK!!!"
                -- logger.debug("\tResult was #{result}")
                _run_ordered_events(remaining_funcs, result)
            return true  -- So that the event succeeds
    })
    event.steamodded_test = true
    G.E_MANAGER::add_event(event)

_n_tabs = (indentation) ->
    string.rep("\t", indentation)

_starts_with = (string, start) -> string::sub(1, #start) == start

-- Meat and potatoes

PREVIOUS_RESULTS_PATH = "steamodded_test_previous_results.json"

context_defaults = {
    indentation: -1
    path: {}
    previous_results: {}
    test_index: 1
    test_count: 1
}
if love.filesystem.getInfo(PREVIOUS_RESULTS_PATH) != nil
    contents = love.filesystem.read(PREVIOUS_RESULTS_PATH)
    if contents != ""
        context_defaults.previous_results = json.decode(contents)

class Status
    new: (@name, @color, @dbp_level, @simplified_result) =>
    passed:  => @simplified_result == "passed"
    failed:  => @simplified_result == "failed"
    skipped: => @simplified_result == "skipped"
    write: (...) => 
        logger.advanced({
            colour: @color,
            level:  string.upper(@dbp_level),
        }, "[test]", ...)

statuses = {x.name, x for x in *{
    Status("LOG",      G.C.JOKER_GREY,                              "info", nil      )
    Status("PASSED",   G.C.GREEN,                                   "info", "passed" )
    Status("FAILED",   G.C.ORANGE,                                  "warn", "failed" )
    Status("ERRORED",  G.C.RED,                                     "warn", "failed" )
    Status("CONFUSED", G.C.EDITION,                                 "warn", "failed" )
    Status("SKIPPED",  mix_colours(G.C.BLACK, G.C.JOKER_GREY, 0.5), "info", "skipped")
}}

export class TestBase
    path_string: (path) -> table.concat(path, "/")
    path_string_tag_yourself: (path) => @.path_string({...path, @name})

    gather_events_config_with_defaults: (config) ->
        defaults = {
            whitelist: ".*",
            failed_only: false
        }
        return {...defaults, ...config}
    
    should_skip: (config, context) => false
    
    gather_events_context_with_defaults: (context) =>
        defaults = context_defaults
        output = {...defaults, ...context}

        return output
    
    extrapolate_subcontext: (context) =>
        subcontext = {...context} -- This isn't a deep copy so be careful :)
        subcontext.indentation += 1
        subcontext.path = {...subcontext.path, @name}  -- to copy the array, does ... instead of appending
        return subcontext
    
    line_text: (context, start_text="", end_text="") =>
        _n_tabs(context.indentation) ..
        "[#{context.test_index}/#{context.test_count}] " ..
        start_text ..
        (start_text != "" and " " or "")..
        @@__name ..
        " \"#{@.path_string(context.path)}\" " ..
        end_text
    
export class Test extends TestBase
    new: (name, funcs) =>
        @name = name
        @funcs = funcs  -- Can be a single function or an array of them
        -- @kwargs = kwargs
        
        -- @kwargs.field_replacements ??= {}  -- Expects object, field_name, new_value

    should_skip: (config, context) => 
        if config.failed_only and context.previous_results[@.path_string(context.path)]
            return "Succeeded last time"
        if config.path and not _starts_with(@.path_string(context.path), config.path)
            return "Not in specified path"
        super(config, context)
    
    gather_events: (config={}, _context={}) =>
        config = @.gather_events_config_with_defaults(config)
        context = @gather_events_context_with_defaults(_context)
        context = @extrapolate_subcontext(context)

        output = _put_in_array_if_alone(@funcs)
        
        output[] = (result) -> 
            if result == "FUCK!!!"
                statuses.ERRORED::write(@line_text(context, nil, "errored! See above..."))
                result = false
            elseif result == false
                statuses.FAILED::write(@line_text(context, nil, "failed! :("))
            elseif result == true
                statuses.PASSED::write(@line_text(context, nil, "passed! :)"))
            else
                statuses.CONFUSED::write(@line_text(context, nil, "returned neither true nor false, but instead #{result}? :S"))
            result
        return output


export class TestBundle extends TestBase
    new: (name, tests) =>
        @name = name
        @tests = tests
    
    run: (config={}) =>
        _run_ordered_events(self\gather_events(config))

    gather_events: (config={}, _context={}) =>
        config = @.gather_events_config_with_defaults(config)
        context = @gather_events_context_with_defaults(_context)
        context = @extrapolate_subcontext(context)

        output = {}
        tally = {passed: 0, failed: 0, skipped: 0}
        output[] = ->
            statuses.LOG::write(_n_tabs(context.indentation) .. "Running test bundle \"#{@.path_string(context.path)}\" (contains #{#@tests} subtest(s))...")
            nil

        context.test_count = #@tests
        for test_index, test in ipairs(@tests)
            context.test_index = test_index
            subcontext = test::extrapolate_subcontext(context)

            events = {}
            skippy = test::should_skip(config, subcontext)
            if skippy
                output[] = (result) -> 
                    tally.skipped += 1
                    statuses.SKIPPED::write(_n_tabs(subcontext.indentation) .. "Skipped \"#{@.path_string(subcontext.path)}\"! Reason: #{skippy}")
                    nil
            else
                events = test\gather_events(config, context)
                output[] = -> 
                    G.E_MANAGER::clear_queue()
                    nil
                for _, event in ipairs(events)
                    output[] = event
                output[] = (result) -> 
                    tally[result and "passed" or "failed"] += 1
                    context.previous_results[@.path_string(subcontext.path)] = result

        output[] = ->
            all_passed = tally.failed == 0
            -- TODO: It can't handle sending Status.SKIPPED as the output if everything was skipped :o
            via = (
            if tally.failed == 0 and tally.passed == 0
                statuses.SKIPPED::write
            elseif all_passed
                statuses.PASSED::write
            else statuses.FAILED::write
            )
            if tally.skipped > 0
                via(_n_tabs(context.indentation) .. "...done with \"#{@.path_string(context.path)}\". Ran #{#@tests - tally.skipped}/#{#@tests} test(s). #{tally.passed} passed, #{tally.failed} failed, #{tally.skipped} skipped.")
            else
                via(_n_tabs(context.indentation) .. "...done with \"#{@.path_string(context.path)}\". Ran #{#@tests} test(s). #{tally.passed} passed, #{tally.failed} failed.")

            if context.indentation == 0 -- This means it's over
                love.filesystem.write(PREVIOUS_RESULTS_PATH, json.encode(context.previous_results))

            return all_passed
        
        output


export run_all_tests = (config={}) ->
    G.E_MANAGER::clear_queue()

    statuses.LOG::write("Running only tests that didn't succeed on their last run...") if config.failed_only

    G.steamodded_tests::run(config)
        
_init = () ->
    G.steamodded_tests = TestBundle("all", {}) if G.steamodded_tests == nil
_init()

-- Assertions
_prepend_space_if_populated = (label) ->
    if label != ""
        label = " " .. label
    label

_abstract_comparison_assertion = (actual, comparison, expectation, explanation="", label="") ->
    explanation = _prepend_space_if_populated(explanation)
    label       = _prepend_space_if_populated(label)
    error_message = "Got #{actual}#{label}, but expected#{explanation} #{expectation}#{label}."
    assert(comparison(actual, expectation), error_message)

--- https://stackoverflow.com/a/32660766
---@param o1 any|table First object to compare
---@param o2 any|table Second object to compare
---@param ignore_mt boolean True to ignore metatables (a recursive function to tests tables inside tables)
_equals = (o1, o2, ignore_mt) ->
    if o1 == o2 then return true
    local o1Type = type(o1)
    local o2Type = type(o2)
    if o1Type ~= o2Type then return false
    if o1Type ~= 'table' then return false

    if not ignore_mt
        local mt1 = getmetatable(o1)
        if mt1 and mt1.__eq
            --compare using built in method
            return o1 == o2

    local keySet = {}

    for key1, value1 in pairs(o1)
        local value2 = o2[key1]
        if value2 == nil or _equals(value1, value2, ignore_mt) == false
            return false
        keySet[key1] = true

    for key2, _ in pairs(o2)
        if not keySet[key2] then return false
    
    return true

export assert_eq     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) ->     _equals(a, b)), expectation, "",                                 label)
export assert_ne     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) -> not _equals(a, b)), expectation, "anything but",                     label)
export assert_lt     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) -> a <  b           ), expectation, "a value less than",                label)
export assert_gt     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) -> a >  b           ), expectation, "a value greater than",             label)
export assert_le     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) -> a <= b           ), expectation, "a value less than or equal to",    label)
export assert_ge     = (actual, expectation, label="") -> _abstract_comparison_assertion(actual, ((a, b) -> a >= b           ), expectation, "a value greater than or equal to", label)

-- See https://gvvaughan.github.io/typecheck/
export assert_type = (object, desired_type) ->
    argcheck('steammodded_test.assert_type', 2, "any", object)
    argcheck('steammodded_test.assert_type', 2, "string", desired_type)
    desired_type_metatable = _G[desired_type]
    argcheck('steammodded_test.assert_type', 2, desired_type, object)
    return true

-- Helpful lil guys :)

export playing_card_from_string = (argscheck 'steamodded_test.main.playing_card_from_string(string) => Card') .. (input) ->
    -- SMODS.create_card seems to be for everyihing but playing cards? Standard and Playing are both not the right set
    suit = "S"
    rank = "T"
    input = string.lower(input)

    if_contains_then = (searching_for, func) ->
        input, count = string.gsub(input, searching_for, "")
        if count > 0
            func()

    -- Suits

    if_contains_then("spades",      -> suit="S")
    if_contains_then("s",           -> suit="S")
    if_contains_then("♠",           -> suit="S")
    if_contains_then("♤",           -> suit="S")

    if_contains_then("hearts",       -> suit="H")
    if_contains_then("h",            -> suit="H")
    if_contains_then("♥",            -> suit="H")
    if_contains_then("♡",            -> suit="H")

    if_contains_then("clubs",       -> suit="C")
    if_contains_then("c",           -> suit="C")
    if_contains_then("♣",           -> suit="C")
    if_contains_then("♧",           -> suit="C")

    if_contains_then("diamonds",    -> suit="D")
    if_contains_then("d",           -> suit="D")
    if_contains_then("♦",           -> suit="D")
    if_contains_then("♢",           -> suit="D")

    -- Ranks

    if_contains_then("ace",         -> rank="A")
    if_contains_then("a",           -> rank="A")

    if_contains_then("king",        -> rank="K")
    if_contains_then("k",           -> rank="K")

    if_contains_then("queen",       -> rank="Q")
    if_contains_then("q",           -> rank="Q")

    if_contains_then("jack",        -> rank="J")
    if_contains_then("j",           -> rank="J")

    if_contains_then("ten",         -> rank="T")
    if_contains_then("10",          -> rank="T")
    if_contains_then("t",           -> rank="T")

    -- eval input="6";string.sub(input, string.find(input, "%d"))
    -- eval string.find("6", "6")
    -- eval string.sub(input, string.find("6", "6")

    a, b = string.find(input, "%d")
    if a != nil
        rank = string.sub(input, a, b)

    x = nil
    y = nil
    if G.play
        x = G.play.T.x + G.play.T.w/2
        y = G.play.T.y
    card = Card(x, y, G.CARD_W, G.CARD_H, nil, G.P_CENTERS.c_base, {playing_card: G.playing_card})
    card::set_base(G.P_CARDS["#{suit}_#{rank}"])
    card

    -- Card(G.play.T.x + G.play.T.w/2, G.play.T.y, G.CARD_W, G.CARD_H, front, G.P_CENTERS.m_stone, {playing_card = G.playing_card})
    -- SMODS.create_card({
    --     set: "Standard",
    --     no_edition: true,
    --     soulable: false
    -- })


-- For actually making useful tests

export waiting_steps = (how_many=1) ->
    output = []
    for _ = 1, how_many
        output[] = -> nil
    output

export new_run = (kwargs={}) ->
    kwargs.stake                ??= 1
    kwargs.seed                 ??= "TUTORIAL"
    kwargs.back                 ??= G.P_CENTERS["b_red"]
    kwargs.silence_back_warning ??= false

    -- TODO this should probably be replaced with some kind of "get center by value" function that can be used elsewhere 
    if type(kwargs.back) == "string"
        if G.P_CENTERS[kwargs.back] != nil
            kwargs.back = G.P_CENTERS[kwargs.back]
        else
            found_it = false
            print(G.P_CENTERS)
            for key, center in pairs(G.P_CENTERS)
                print(center.name)
                if center.name != kwargs.back
                    continue
                found_it = true
                unless kwargs.silence_back_warning
                    logger.warn("Found Back ID \"#{key}\" from the name \"#{center.name}\", you might want to use the ID instead to precent conflicts. Turn this warning off with kwargs.silence_back_warning")
                kwargs.back = center
            if not found_it
                error("Provided string \"#{kwargs.back}\" didn't seem to match any back names or IDs.")

    if kwargs.back != nil
        G.GAME.selected_back = kwargs.back

    G.FUNCS.start_run(nil, {
        stake: kwargs.stake,
        seed:  kwargs.seed,
    })

export new_round = () ->
    ui_map.screens.select_blind.small.buttons.get_select()::click()
    
-- macro ok = () -> '_module_0["new_round"] = _new_round'
-- $ok

export create_state_steps = (kwargs={}) ->
    return {
        -> new_run(kwargs),
        -> new_round(),
    }

-- export new_shop = () ->
--     -- G.round_eval = {}  -- cash_out expects this to not be nil
--     end_round()
--     G.STATE = G.STATES.SHOP
--     G.FUNCS.cash_out()

export empty_hand = ->
    G.hand.cards = {}

export empty_centers = ->
    G.jokers.cards = {}
    G.consumeables.cards = {}

export add_cards_to_hand = (playing_cards, select_them=false) ->
    for _, card in ipairs(playing_cards)
        if type(card) == "string"
            card = playing_card_from_string(card)
        card\add_to_deck()
        G.deck.config.card_limit += 1
        table.insert(G.playing_cards, card)
        G.hand\emplace(card)
        if select_them
            card\click!

export add_centers = (center_keys) ->
    center_keys = _put_in_array_if_alone(center_keys)
    added_centers = {}
    for _, center_key in ipairs(center_keys)
        added_centers[] = SMODS.add_card({key: center_key, soulable: false, no_edition: true})
    return added_centers

export open_arcana_pack = () ->
    pack = add_centers("p_arcana_normal_2")[1]
    pack.cost = 0
    pack::open()

export play_hand = (playing_cards, kwargs={})->
    kwargs.hold =    kwargs.hold or {}
    kwargs.centers = kwargs.centers or {}

    empty_hand()
    empty_centers()
    add_cards_to_hand(playing_cards, true)
    add_cards_to_hand(kwargs.hold)
    add_centers(kwargs.centers)
    
    G.FUNCS.play_cards_from_highlighted()

export discard_something_steps = () -> 
    return {
        -> ui_map.card_areas.get_hand().cards[1]::click()
        -> ui_map.screens.blind.buttons.get_discard()::click()
    }

-- So this seems stupid, but...
-- It results in many functions. At time of writing, it's these, but this is open to change:
-- assert_hand_scored = (compare_chips_to) -> ...
-- assert_hand_scored_ne = (compare_chips_to) -> ...
-- assert_hand_scored_lt = (compare_chips_to) -> ...
-- assert_hand_scored_gt = (compare_chips_to) -> ...
-- assert_hand_scored_le = (compare_chips_to) -> ...
-- assert_hand_scored_ge = (compare_chips_to) -> ...
-- assert_dollars = (compare_dollars_to) -> ...
-- assert_dollars_ne = (compare_dollars_to) -> ...
-- assert_dollars_lt = (compare_dollars_to) -> ...
-- assert_dollars_gt = (compare_dollars_to) -> ...
-- assert_dollars_le = (compare_dollars_to) -> ...
-- assert_dollars_ge = (compare_dollars_to) -> ...
macro define_many_assertion_functions =  -> 
    output = ""

    for _, assertion_group in ipairs({
        {function_append: "hand_scored", value_label: "chips",   value_source: "G.GAME.chips"  },
        {function_append: "dollars",     value_label: "dollars", value_source: "G.GAME.dollars"},
    })
        for _, assertion_operation in ipairs({
            {internal_function_name: "assert_eq", external_function_append: ""   },
            {internal_function_name: "assert_ne", external_function_append: "_ne"},
            {internal_function_name: "assert_lt", external_function_append: "_lt"},
            {internal_function_name: "assert_gt", external_function_append: "_gt"},
            {internal_function_name: "assert_le", external_function_append: "_le"},
            {internal_function_name: "assert_ge", external_function_append: "_ge"},
        })
            -- You can't export like this, but you *can* assign to _module_0, which seemingly does the same thing
            output ..= "\n_module_0[\"assert_#{assertion_group.function_append}#{assertion_operation.external_function_append}\"] = (compare_#{assertion_group.value_label}_to) -> #{assertion_operation.internal_function_name}(#{assertion_group.value_source}, compare_#{assertion_group.value_label}_to, '#{assertion_group.value_label}')"
            --ex:       \n_module_0[\"assert_#{hand_scored                    }#{_lt                                         }\"] = (compare_#{chips                      }_to) -> #{assert_lt                                 }(#{G.GAME.chips                }, compare_#{chips                      }_to, '#{chips                      }')"
            --          export assert_hand_scored_lt = (compare_chips_to) -> assert_lt(G.GAME.chips, compare_chips_to, 'chips')
    output

$define_many_assertion_functions

export assert_valid_ui = (ui, lenient=false, preceeding_path="ui", path_depth=0) ->
    print(ui)
    element_names = {}
    -- This is gross but yue didn't like when I just defined them with keys (probably because they're numbers?)
    element_names[1] = "text (G.UIT.T)"
    element_names[2] = "box (G.UIT.B)"
    element_names[3] = "column (G.UIT.C)"
    element_names[4] = "row (G.UIT.R)"
    element_names[5] = "object (G.UIT.O)"
    element_names[7] = "root (G.UIT.ROOT)"
    element_names[8] = "slider (G.UIT.S)"
    element_names[9] = "input text box (G.UIT.I)"
    element_names[0] = "default padding (G.UIT.padding)"

    error_with_path = (text) -> error("In #{preceeding_path}: #{text}", 2+path_depth)
    require_config_option = (option_name) -> if ui.config?[option_name] == nil then error_with_path("Required config option for #{element_names[ui.n]}: #{option_name}")
    
    assert_config_type = (path, value, type_name) -> 
        if type(value) != nil and type(value) != type_name then error_with_path("Wrong type for #{path}: Expected a #{type_name}, got #{value}, a #{type(value)}")

    switch ui.n
        when nil
            error_with_path("No node type specified")
        when G.UIT.T
            require_config_option("colour")
            require_config_option("text")
            require_config_option("scale")
        when G.UIT.B
            require_config_option("w")
            require_config_option("h")
        when G.UIT.C
            -- nothing :) (required for compile)
        when G.UIT.R
            -- nothing :) (required for compile)
        when G.UIT.O
            -- nothing :) (required for compile)
        when G.UIT.ROOT
            -- nothing :) (required for compile)
        when G.UIT.S
            -- nothing :) (required for compile)
        when G.UIT.I
            -- nothing :) (required for compile)
        when G.UIT.padding
            -- nothing :) (required for compile)
        else
            error_with_path("Unknown node type (#{ui.n})")
    
    -- macro WAIT_CAN_I_JUST = (s) -> "{\"config.#{s}\", ui.config?.#{s}}"
    -- assert_config_type(unpack($WAIT_CAN_I_JUST align), "string")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST w), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST minw), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST maxw), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST h), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST minh), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST maxh), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST padding), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST r), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST colour), "table")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST colour?[1]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST colour?[2]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST colour?[3]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST colour?[4]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline_colour), "table")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline_colour?[1]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline_colour?[2]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline_colour?[3]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline_colour?[4]), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST no_fill), "boolean")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST outline), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST emboss), "number")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST hover), "boolean")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST shadow), "boolean")
    -- assert_config_type(unpack($WAIT_CAN_I_JUST juice), "boolean")

    if ui.nodes
        for node_index, node in ipairs(ui.nodes)
            assert_valid_ui(node, lenient, "#{preceeding_path}.nodes[#{node_index}]", path_depth+1)

            if not lenient  and ui.n == G.UIT.C and node.n == G.UIT.B and #ui.nodes != 1
                error_with_path("Balatro's UI gets buggy if you have text that isn't the only node in its column (and maybe rows too sometimes idk?)")
    
    return ui

-- DebugPlus stuff


config_next_token = (context) ->
    token = context.tokens[context.token_index]
    context.token_index += 1
    return token

subcommands = {
    failed: {
        function: (context) -> context.config.failed_only = true,
        description: "Runs only tests that didn't succeed the last time they were ran (this includes new tests that didn't succeed last time because there wasn't a last time)"
    }
    path: {
        function: (context) -> 
            context.config.path = config_next_token(context)
            if context.config.path == nil
                error("You need to specify a path!! :o")
            if not _starts_with(context.config.path, "all/")
                context.config.path = "all/" .. context.config.path
        description: "Runs only tests within the specified path. \"all/\" is automatically prepended if not added manually."
    }
}
for subcommand_name, subcommand in pairs(subcommands)
    subcommand.aliases = {
        "-"..string.sub(subcommand_name,1,1)
        "--"..subcommand_name
    }

config_from_string = (input_string) ->
    context = {
        tokens: [x for x in string.gmatch(input_string, "[^%s]+")]
        token_index: 1
        config: {}
    }

    while context.token_index <= #context.tokens
        token = config_next_token(context)
        found = false
        for subcommand_name, subcommand in pairs(subcommands)
            for alias in *subcommand.aliases
                if token != alias then continue
                subcommand.function(context)
                found = true
        if not found
            error("Unknown token: #{token}")
        
    return context.config

if dbp_success and dpAPI.isVersionCompatible(1) and debugplus
    desc_lines = {}
    for subcommand_name, subcommand in pairs(subcommands)
        desc_lines[] = {
            table.concat(subcommand.aliases, ', ')
            subcommand.description
        }
    aliases_max_width = math.max(unpack [#desc_line[1] for desc_line in *desc_lines])

    desc = "IDK man!!! It runs your tests and tells you the results!! has subcommand(s) tho :)\n"
    desc ..= table.concat(
        ["\t" .. desc_line[1] .. string.rep(" ", aliases_max_width + 3 - #desc_line[1]) .. desc_line[2] for desc_line in *desc_lines],  -- This has to be on one line it seems which is kinda annoying
        "\n"
    )

    success, result = pcall(debugplus.addCommand, {
        name: "test",
        shortDesc: "Runs all tests. uh, unless you make it do otherwise.",
        desc: desc,
        exec: (args, raw_args, dp) ->
            config = config_from_string(raw_args)  -- Uses raw args because I want to eventually decouple this from dbp
            run_all_tests(config)
            "here we go :)"
    })
    if (not success) and (not string.match(result, "This command already exists$"))
        error(result)
    
    dbp_console_dot_lua = assert(SMODS.load_file('debugplus/console.lua', "DebugPlus"))()